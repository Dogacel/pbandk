buildscript {
    ext.kotlin_version = '1.3.11'
    repositories {
        mavenCentral()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.7'
        classpath 'org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.44'
    }
}

apply plugin: 'kotlin2js'
apply plugin: 'kotlin-dce-js'
apply plugin: 'org.jetbrains.kotlin.frontend'
apply plugin: 'com.google.protobuf'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile 'com.github.streem.pb-and-k:pbandk-runtime-js:0.7.0'
    // This is needed for includes, ref: https://github.com/google/protobuf-gradle-plugin/issues/41#issuecomment-143884188
    compileOnly 'com.google.protobuf:protobuf-java:3.6.1'
}

compileJava.enabled = false

compileKotlin2Js {
    // Generate the protobuf files before compilation
    dependsOn 'generateProto'
    kotlinOptions.metaInfo = true
    kotlinOptions.sourceMap = true
    kotlinOptions.moduleKind = 'commonjs'
}

kotlinFrontend {
    downloadNodeJsVersion = 'latest'

    npm {
        dependency('protobufjs', '^6.8.8')
    }

    webpackBundle {
        bundleName = 'main'
        contentPath = file('src/main/web')
        mode = 'production'
    }
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src"
    protoc {
        artifact = 'com.google.protobuf:protoc:3.6.1'
    }
    plugins {
        kotlin {
            artifact = 'com.github.streem.pb-and-k:protoc-gen-kotlin-jvm:0.7.0:jvm8@jar'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                kotlin {
                    option 'kotlin_package=pbandk.examples.browserjs.pb'
                }
            }
        }
    }
}
