buildscript {
    ext.kotlin_serialization_version = '0.20.0'
    ext.pbandk_version = 'master-SNAPSHOT'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72"
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.3.72"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.12'
    }
}

import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: 'application'
apply plugin: 'com.google.protobuf'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

mainClassName = 'pbandk.examples.addressbook.MainKt'
applicationName = 'addressbook'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlin_serialization_version"
    implementation "com.github.streem.pbandk:pbandk-runtime-jvm:$pbandk_version"
}

compileJava.enabled = false

protobuf {
    generatedFilesBaseDir = "$projectDir/src"
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.1'
    }
    plugins {
        kotlin {
            artifact = "com.github.streem.pbandk:protoc-gen-kotlin-jvm:$pbandk_version:jvm8@jar"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                kotlin {
                    option 'kotlin_package=pbandk.examples.addressbook.pb'
                }
            }
        }
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

// Workaround the Gradle bug resolving multiplatform dependencies.
// Fix courtesy of https://github.com/square/okio/issues/647
configurations.all { configuration ->
    if (name.toLowerCase().contains('kapt') || name.toLowerCase().contains('proto')) {
        attributes.attribute(Usage.USAGE_ATTRIBUTE, project.objects.named(Usage.class, Usage.JAVA_RUNTIME))
    }
}
