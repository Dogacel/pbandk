buildscript {
    ext.kotlin_version = '1.3.11'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.7'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.google.protobuf'

mainClassName = 'pbandk.examples.addressbook.MainKt'
applicationName = 'addressbook'

dependencies {
    compile 'com.github.streem.pb-and-k:pbandk-runtime-jvm:0.5.0'
}

compileJava.enabled = false

protobuf {
    generatedFilesBaseDir = "$projectDir/src"
    protoc {
        artifact = 'com.google.protobuf:protoc:3.6.1'
    }
    plugins {
        kotlin {
            artifact = 'com.github.streem.pb-and-k:protoc-gen-kotlin-jvm:0.5.0:jvm8@jar'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                kotlin {
                    option 'kotlin_package=pbandk.examples.addressbook.pb'
                }
            }
        }
    }
}
